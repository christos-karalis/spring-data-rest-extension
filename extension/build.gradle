group 'net.sourceforge'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: "jacoco"

ext {
    queryDslVersion = '4.1.0'
    hibernateVersion = '5.2.10.Final'
    springVersion = '4.3.8.RELEASE'
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/test/generated']
        }
    }
    test {
        java {
            srcDirs = ['src/test/generated', 'src/test/java']
        }
    }
}

configurations {
    querydslapt
}

repositories {
    mavenCentral()
}

dependencies {

    querydslapt group:'com.querydsl',         name:'querydsl-apt',            version:queryDslVersion

    compile group:'org.hibernate',            name:'hibernate-core',          version: hibernateVersion
    compile group:'org.hibernate',            name:'hibernate-entitymanager', version: hibernateVersion
    compile group:'org.hibernate',            name:'hibernate-ehcache',       version: hibernateVersion

    compile group:'org.codehaus.groovy',      name:'groovy-all',              version:'2.4.11'
    compile group:'org.springframework.data', name:'spring-data-jpa',         version:'1.11.3.RELEASE'
    compile group:'org.springframework.data', name:'spring-data-rest-webmvc', version:'2.6.3.RELEASE'
    compile group:'org.apache.commons',       name:'commons-lang3',           version:'3.1'

    compile group:'com.querydsl',             name:'querydsl-core',           version:queryDslVersion
    compile group:'com.querydsl',             name:'querydsl-sql',            version:queryDslVersion
    compile group:'com.querydsl',             name:'querydsl-jpa',            version:queryDslVersion

    compile 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile group:'org.springframework',  name:'spring-orm',              version:springVersion
    testCompile group:'org.springframework',  name:'spring-test',             version:springVersion
    testCompile group:'org.springframework',  name:'spring-jdbc',             version: springVersion
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.2'
//    testCompile group:'org.apache.derby',     name:'derby',                   version:'10.11.1.1'
    testCompile group: 'junit',               name: 'junit',                  version:'4.12'
}


jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}


clean {
    delete sourceSets.generated.java.srcDirs
}

task generateQueryDSL(type: org.gradle.api.tasks.compile.JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.test.java
    classpath = configurations.compile + configurations.querydslapt + configurations.testCompile + sourceSets.main.output
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

check {
    dependsOn generateQueryDSL
}

test.mustRunAfter jacocoTestReport

compileTestJava {
    source += sourceSets.generated.java

}