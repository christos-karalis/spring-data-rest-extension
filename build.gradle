group 'net.sourceforge'
version '1.0-SNAPSHOT'

allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'groovy'
}

apply plugin: 'war'

ext {
    queryDslVersion = '4.1.0'
    hibernateVersion = '5.2.10.Final'
    springVersion = '4.3.8.RELEASE'
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/test/generated']
        }
    }
    test {
        java {
            srcDirs = ['src/test/generated', 'src/test/java']
        }
    }
}

configurations {
    querydslapt
}

repositories {
    mavenCentral()
}

dependencies {

    querydslapt group:'com.querydsl',         name:'querydsl-apt',            version:queryDslVersion

    compile group:'org.hibernate',            name:'hibernate-core',          version: hibernateVersion
    compile group:'org.hibernate',            name:'hibernate-entitymanager', version: hibernateVersion
    compile group:'org.hibernate',            name:'hibernate-ehcache',       version: hibernateVersion

    compile group:'org.codehaus.groovy',      name:'groovy-all',              version:'2.4.11'
    compile group:'org.springframework.data', name:'spring-data-jpa',         version:'1.11.3.RELEASE'
    compile group:'org.springframework.data', name:'spring-data-rest-webmvc', version:'2.6.3.RELEASE'
    compile group:'org.apache.commons',       name:'commons-lang3',           version:'3.1'

    compile group:'com.querydsl',             name:'querydsl-core',           version:queryDslVersion
    compile group:'com.querydsl',             name:'querydsl-sql',            version:queryDslVersion
    compile group:'com.querydsl',             name:'querydsl-jpa',            version:queryDslVersion

    compile 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile group:'org.springframework',  name:'spring-orm',              version:springVersion
    testCompile group:'org.springframework',  name:'spring-test',             version:springVersion
    testCompile group:'org.springframework',  name:'spring-jdbc',             version: springVersion
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.2'
    testCompile group: 'junit',               name: 'junit',                  version:'4.12'
}

clean {
    delete sourceSets.generated.java.srcDirs
}


task generateQueryDSL(type: org.gradle.api.tasks.compile.JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.test.java
    classpath = configurations.compile + configurations.querydslapt + configurations.testCompile
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        println it.sourceSets.main
        sourceSets it.sourceSets.main
    }

    println sourceSets

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled true
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

compileTestJava {
    source += sourceSets.generated.java
}
