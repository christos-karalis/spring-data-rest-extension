group 'net.sourceforge'
version '1.0-SNAPSHOT'

apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

configurations {
    querydslapt
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

ext {
    queryDslVersion = '4.1.0'
    hibernateVersion = '5.2.10.Final'
    springVersion = '4.1.2.RELEASE'
    springWsVersion = '2.4.0.RELEASE'
}

clean {
    delete sourceSets.generated.java.srcDirs
}


buildscript {
    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }
    repositories {
        jcenter()
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    def tomcatVersion = '7.0.59'

    compile project(':extension')
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.2'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    querydslapt group:'com.querydsl', name:'querydsl-apt', version:queryDslVersion

    testCompile group:'org.springframework',  name:'spring-orm',              version:springVersion
    testCompile group:'org.springframework',  name:'spring-test',             version:springVersion
    testCompile group:'org.springframework',  name:'spring-jdbc',             version: springVersion
    testCompile group:'com.jayway.jsonpath', name:'json-path', version:'0.9.1'
    testCompile group:'com.jayway.jsonpath', name:'json-path-assert', version:'0.9.1'
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.2'
//    testCompile group:'org.apache.derby',     name:'derby',                   version:'10.11.1.1'
    testCompile group: 'junit',               name: 'junit',                  version:'4.12'

    tomcat 'org.apache.tomcat.embed:tomcat-embed-core:'+tomcatVersion,
            'org.apache.tomcat.embed:tomcat-embed-logging-juli:'+tomcatVersion,
            'org.apache.tomcat.embed:tomcat-embed-jasper:'+tomcatVersion,
            'org.apache.tomcat:tomcat-dbcp:'+tomcatVersion,
            'org.apache.tomcat:tomcat-jdbc:'+tomcatVersion
}

task generateQueryDSL(type: org.gradle.api.tasks.compile.JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

